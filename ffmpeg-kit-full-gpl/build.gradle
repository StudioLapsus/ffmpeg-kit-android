apply plugin: 'com.android.library'

android {
    namespace 'com.arthenica.ffmpegkit'
    compileSdkVersion 35
    buildToolsVersion "35.0.0"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 600
        versionName "6.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

// Package the prebuilt AAR
android.libraryVariants.all { variant ->
    if (variant.buildType.name == 'release') {
        variant.outputs.all {
            outputFileName = "ffmpeg-kit-full-gpl-6.0.0.aar"
        }
    }
}

dependencies {
    implementation(name: 'ffmpeg-kit-full-gpl', ext: 'aar')
    implementation 'com.arthenica:smart-exception-java:0.2.1'
    implementation 'com.arthenica:smart-exception-common:0.2.1'
}

// This is required for JitPack
apply plugin: 'maven-publish'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.StudioLapsus'
                artifactId = 'ffmpeg-kit-android'
                version = '6.0.0'
            }
        }
    }
} 