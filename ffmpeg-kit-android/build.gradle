apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace 'com.arthenica.ffmpegkit'
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 32
        versionCode 611
        versionName "6.0.11"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation 'com.arthenica:smart-exception-java:0.2.1'
    implementation 'com.arthenica:smart-exception-common:0.2.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.StudioLapsus'
                artifactId = 'ffmpeg-kit-android'
                version = '6.0.11'
                
                pom {
                    name = 'FFmpeg Kit Android'
                    description = 'FFmpeg Kit for Android applications'
                    url = 'https://github.com/StudioLapsus/ffmpeg-kit-android'
                    
                    licenses {
                        license {
                            name = 'GPL-3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        }
                    }
                    
                    developers {
                        developer {
                            id = 'StudioLapsus'
                            name = 'Studio Lapsus'
                        }
                    }
                    
                    scm {
                        connection = 'scm:git:git://github.com/StudioLapsus/ffmpeg-kit-android.git'
                        developerConnection = 'scm:git:ssh://github.com:StudioLapsus/ffmpeg-kit-android.git'
                        url = 'https://github.com/StudioLapsus/ffmpeg-kit-android'
                    }
                }
                
                pom.withXml {
                    def dependenciesNode = asNode().dependencies[0]
                    if (dependenciesNode != null) {
                        // Remove the ffmpeg-kit dependency that has no groupId - classes should be bundled
                        def toRemove = dependenciesNode.dependency.findAll { dep ->
                            dep.artifactId?.text() == 'ffmpeg-kit' && !dep.groupId?.text()
                        }
                        toRemove.each { dependenciesNode.remove(it) }
                    }
                }
            }
        }
    }
    
    // Disable all module metadata generation
    tasks.matching { it.name.startsWith('generateMetadataFileFor') }.all {
        enabled = false
    }
    tasks.matching { it.name.startsWith('generatePomFileFor') && !it.name.contains('Release') }.all {
        enabled = false  
    }
}